import arc.util.*
import ent.*
import mindustry.client.*
import mindustry.client.service.*
import mindustry.client.task.*
import java.util.jar.*

buildscript {
	val (mindustry, mindustryVersion, mindustrySource) = when(val version = providers.gradleProperty("mindustryVersion").get()) {
		"latest" -> Triple("Mindustry", "latest", "Anuken/Mindustry/releases/latest/download/dependencies.jar")
		"be" -> Triple("MindustryBuilds", "latest", "Anuken/MindustryBuilds/releases/download/master/latest.jar")
		else -> Triple("Mindustry", version, "Anuken/Mindustry/releases/download/[revision]/dependencies.jar")
	}

	dependencies {
		classpath("Anuken:$mindustry:$mindustryVersion")
	}

	configurations.configureEach {
		// Resolve the correct Mindustry dependency.
		resolutionStrategy.eachDependency {
			if (requested.group == "Anuken" && requested.name.startsWith("Mindustry")) {
				useTarget("Anuken:$mindustry:$mindustryVersion")
			}
		}
	}

	repositories {
		ivy {
			url = uri("https://github.com")
			patternLayout {
				artifact(mindustrySource)
				metadataSources{ artifact() }
			}
			content {
				includeVersion("Anuken", mindustry, mindustryVersion)
			}
		}
	}
}

plugins {
	java
	id("com.github.GglLfr.EntityAnno") apply false
	id("com.github.GglLfr.MindustryClient") apply false
}

val (mindustry, mindustryVersion, mindustrySource) = when(val version = providers.gradleProperty("mindustryVersion").get()) {
	"latest" -> Triple("Mindustry", "latest", "Anuken/Mindustry/releases/latest/download/dependencies.jar")
	"be" -> Triple("MindustryBuilds", "latest", "Anuken/MindustryBuilds/releases/download/master/latest.jar")
	else -> Triple("Mindustry", version, "Anuken/Mindustry/releases/download/[revision]/dependencies.jar")
}

val entVersion = providers.gradleProperty("entVersion").get()

val modArtifact = providers.gradleProperty("modArtifact").get()
val modFetch = providers.gradleProperty("modFetch").get()
val modGenSrc = providers.gradleProperty("modGenSrc").get()
val modGen = providers.gradleProperty("modGen").get()

val modVersion = providers.gradleProperty("modVersion").get()
val alpha = providers.gradleProperty("alpha").get().toBoolean()

fun jarName(type: String): String {
	return "$modArtifact$type-${modVersion + (if (alpha) "-alpha" else "")}.jar"
}

allprojects {
	apply(plugin = "java")
	sourceSets["main"].java.setSrcDirs(listOf(
		layout.projectDirectory.dir("src"),
		layout.projectDirectory.dir("build/generated/sources/annotationProcessor/java/main")
	))

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	dependencies {
		registerTransform(TrimSources::class) {
			from.attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, ArtifactTypeDefinition.JAR_TYPE)
			to.attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, "jar-stripped")
		}
	}

	configurations.configureEach {
		// Resolve the correct Mindustry dependency.
		resolutionStrategy.eachDependency {
			if (requested.group == "Anuken" && requested.name.startsWith("Mindustry")) {
				useTarget("Anuken:$mindustry:$mindustryVersion")
			}
		}

		if (isCanBeResolved) attributes {
			attribute(ArtifactTypeDefinition.ARTIFACT_TYPE_ATTRIBUTE, "jar-stripped")
		}
	}

	repositories {
		// Use Ivy repository for Mindustry builds.
		ivy {
			url = uri("https://github.com")
			patternLayout {
				artifact(mindustrySource)
				metadataSources{ artifact() }
			}
			content {
				includeVersion("Anuken", mindustry, mindustryVersion)
			}
		}

		// Necessary Maven repositories to pull dependencies from.
		mavenLocal()
		mavenCentral()
		maven("https://oss.sonatype.org/content/repositories/snapshots/")
		maven("https://oss.sonatype.org/content/repositories/releases/")
		maven("https://raw.githubusercontent.com/GglLfr/EntityAnnoMaven/main")
	}

	tasks.withType<JavaCompile>().configureEach {
		options.apply {
			compilerArgs.add("-Xlint:-options")
			compilerArgs.add("-implicit:none")
			compilerArgs.addAll(providers.gradleProperty("org.gradle.jvmargs").get()
				.split(Regex("\\s+"))
				.filter{ it.startsWith("--add-opens") }
				.map{ "--add-exports=${it.substring("--add-opens=".length)}" }
			)

			isIncremental = true
			isFork = false
			encoding = "UTF-8"
		}
	}
}

project(":") {
	apply(plugin = "com.github.GglLfr.EntityAnno")
	apply(plugin = "com.github.GglLfr.MindustryClient")

	configure<EntityAnnoExtension> {
		revisionDir = layout.projectDirectory.dir("revisions")
		fetchPackage = modFetch
		genSrcPackage = modGenSrc
		genPackage = modGen
	}

	dependencies {
		// Use the entity generation annotation processor.
		compileOnly("com.github.GglLfr.EntityAnno:entity:$entVersion")
		annotationProcessor("com.github.GglLfr.EntityAnno:entity:$entVersion")

		compileOnly("Anuken:$mindustry:$mindustryVersion")
	}

	val client = gradle.sharedServices.registerIfAbsent(MindustryClientPlugin.serviceName, MindustryClientService::class.java) { }
	val jar = tasks.named<Jar>("jar") {
		dependsOn(":tools:generate")

		archiveFileName = jarName("Desktop")

		from(
			files(sourceSets["main"].output.classesDirs),
			files(sourceSets["main"].output.resourcesDir),
			configurations.runtimeClasspath.map{ conf -> conf.map{ if (it.isDirectory) it else zipTree(it) }},

			files(layout.projectDirectory.dir("assets")),
			layout.projectDirectory.file("icon.png"),

			// Check both JSON and HJSON.
			layout.projectDirectory.file("mod.json"),
			layout.projectDirectory.file("mod.hjson")
		)

		metaInf.from(layout.projectDirectory.file("LICENSE"))
	}

	val dex = tasks.register<Jar>("dex") {
		description = "Builds an Android-compatible JAR from the desktop-only JAR. Use this file for GitHub release."

		val androidSdkVersion = providers.gradleProperty("androidSdkVersion").get()
		val androidBuildVersion = providers.gradleProperty("androidBuildVersion").get()
		val androidMinVersion = providers.gradleProperty("androidMinVersion").get()

		val classpaths = files(configurations.compileClasspath, configurations.runtimeClasspath)
		inputs.files(jar)
		inputs.files(classpaths)
		inputs.property("androidSdk", "$androidSdkVersion+$androidBuildVersion+$androidMinVersion")

		archiveFileName = jarName("")

		val desktopJar = jar.flatMap{ it.archiveFile }
		val dexJar = File(temporaryDir, "Dex.jar")
		val providers = project.providers

		from(zipTree(desktopJar), zipTree(dexJar))
		doFirst {
			// Find Android SDK root.
			val sdkRoot = File(
				OS.env("ANDROID_HOME") ?: OS.env("ANDROID_SDK_ROOT")
				?: throw GradleException("Neither `ANDROID_HOME` nor `ANDROID_SDK_ROOT` are set.")
			)

			// Find `d8`.
			val d8 = File(sdkRoot, "build-tools/$androidBuildVersion/${if (OS.isWindows) "d8.bat" else "d8"}")
			if (!d8.exists()) throw GradleException("Android SDK `build-tools;$androidBuildVersion` isn't installed or is corrupted.")

			// Initialize a release build.
			val input = desktopJar.get().asFile
			val command = arrayListOf("$d8", "--release", "--min-api", androidMinVersion, "--output", "$dexJar", "$input")

			// Include all compile and runtime classpath.
			classpaths.forEach {
				if (it.exists()) command.addAll(arrayOf("--classpath", it.path))
			}

			// Include Android platform as library.
			val androidJar = File(sdkRoot, "platforms/android-$androidSdkVersion/android.jar")
			if (!androidJar.exists()) throw GradleException("Android SDK `platforms;android-$androidSdkVersion` isn't installed or is corrupted.")

			command.addAll(arrayOf("--lib", "$androidJar"))
			if (OS.isWindows) command.addAll(0, arrayOf("cmd", "/c").toList())

			// Run `d8`.
			providers.exec{ commandLine(command) }.result.get().rethrowFailure()
		}
	}

	val copyJar = tasks.register<DefaultTask>("copyJar") {
		description = "Copies the desktop JAR to the `mods/` folder."
		mustRunAfter(jar)
		usesService(client)

		val jarName = jar.flatMap{ it.archiveFileName }
		val dexName = dex.flatMap{ it.archiveFileName }

		doLast {
			val source = layout.projectDirectory.file("build/libs/${jarName.get()}").asFile

			if (!source.exists()) {
				logger.lifecycle("JAR `${source.path}` not found. Skipping task.")
				return@doLast
			}

			val destination = client.get().detected.modsDirectory
			destination.mkdirs()
			destination.resolve(dexName.get()).delete()

			source.copyTo(destination.resolve(source.name), overwrite = true)

			logger.lifecycle("Copied ${source.name} to `${destination.path}`.")
		}
	}

	tasks.withType<RunClientTask>().configureEach {
		dependsOn(copyJar)
	}

	tasks.register("runClient") {
		dependsOn(tasks.named("run"))
		description = "Alias for `run` task."
	}
}

abstract class TrimSources : TransformAction<TransformParameters.None> {
	@get:InputArtifact
	abstract val file: Provider<FileSystemLocation>

	override fun transform(outputs: TransformOutputs) {
		val input = file.get().asFile
		val classes = outputs.file(input.name)

		JarFile(input).use{ jar ->
			val entries = jar.entries()
			classes.outputStream().use{ JarOutputStream(it).use{ classes ->
				for (entry in entries) {
					if (entry.name.endsWith(".java")) continue

					classes.putNextEntry(JarEntry(entry.name))
					jar.getInputStream(entry).use{ jar -> jar.copyTo(classes) }
					classes.closeEntry()
				}
			}}
		}
	}
}